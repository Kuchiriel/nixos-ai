{pkgs, ...}: {
  security.rtkit.enable = true;
  services.pipewire = {
    enable = true;
    alsa.enable = true;
    alsa.support32Bit = true;
    pulse.enable = true;

    # Denoise RNNoise (werman/noise-suppression-for-voice via nixpkgs
    # `rnnoise-plugin`): cria o source virtual `rnnoise_source` com supressão
    # de ruído em tempo real (ventoinha + sons de casa do notebook).
    # O wakeword/STT apontam para esse source em vez do device cru.
    # Parâmetros VAD do legado (calibrados em ambiente ruidoso):
    #   - VAD Threshold 50% (fala vs. ruído)
    #   - VAD Grace 200ms (não corta início de palavra)
    extraConfig.pipewire = {
      "99-input-denoising" = {
        "context.modules" = [
          {
            name = "libpipewire-module-filter-chain";
            args = {
              "node.description" = "Noise Canceling source";
              "media.name" = "Noise Canceling source";
              "filter.graph" = {
                nodes = [
                  {
                    type = "ladspa";
                    name = "rnnoise";
                    plugin = "${pkgs.rnnoise-plugin}/lib/ladspa/librnnoise_ladspa.so";
                    label = "noise_suppressor_mono";
                    control = {
                      "VAD Threshold (%)" = 50.0;
                      "VAD Grace Period (ms)" = 200;
                      "Retroactive VAD Grace (ms)" = 0;
                    };
                  }
                ];
              };
              "capture.props" = {
                "node.name" = "capture.rnnoise_source";
                "node.passive" = true;
                "audio.rate" = 48000;
              };
              "playback.props" = {
                "node.name" = "rnnoise_source";
                "media.class" = "Audio/Source";
                "audio.rate" = 48000;
              };
            };
          }
        ];
      };
    };
  };
}
